plugins {
	id 'fabric-loom'
}

if(rootProject.file('private.gradle').exists()) { //Publishing details
	apply from: rootProject.file('private.gradle')
}

archivesBaseName = "LibGuiTest"
version = rootProject.version
group = rootProject.group

loom {
	runConfigs.configureEach {
		ideConfigGenerated = true
	}
}

repositories {
	maven { url "https://server.bbkr.space/artifactory/libs-release" }
	maven {
		url = "https://maven.terraformersmc.com/releases"
		content {
			includeGroup "com.terraformersmc"
		}
	}
	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
	mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
	modApi "net.fabricmc:fabric-loader:${rootProject.loader_version}"
	modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"

	compileOnly ("com.google.code.findbugs:jsr305:3.0.2") { transitive = false }

	implementation project(path: ':', configuration: 'namedElements')
	// TODO: Fix this in Loom (namedElements should expose remapped mod* dependencies)
	modImplementation "io.github.cottonmc:Jankson-Fabric:${rootProject.jankson_version}"
	modLocalRuntime("com.terraformersmc:modmenu:$rootProject.modmenu_version") {
		exclude group: 'net.fabricmc.fabric-api'
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	options.encoding = "UTF-8"
	options.release.set 16
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}
